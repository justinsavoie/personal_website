---
output: html_document
---

```{r setup, include=FALSE}
library(tidyverse)
library(rstan)
library(Metrics)

to_model <- read_csv("~/Dropbox (Personal)/nhl_stan/dynamic_hierarchical/data.csv")
unique_nhl_teams <- sort(unique(to_model$Visitor))

nt <- length(unique_nhl_teams)
ng <- nrow(to_model)
ht <- match(to_model$Home,unique_nhl_teams)
at <- match(to_model$Visitor,unique_nhl_teams)
s1 <- to_model$G_Home
s2 <- to_model$G_Visitor

model_data <- list(nt=nt,ng=ng,ht=ht,at=at,s1=s1,s2=s2)
```

### Dynamic hierarchical modeling in Stan for predicting sports games

This post is about hierarchical modeling in Stan for predicting sports games. It's inspired by this excellent [Stan tutorial](https://www.youtube.com/playlist?list=PLCrWEzJgSUqwL85xIj1wubGdY15C5Gf7H) by Maggie Lieu on the same subject (github link [here](https://github.com/MaggieLieu/STAN_tutorials/tree/master/Hierarchical)).

The original model is relatively simple, each team has an "attack strength" and a "defense strength". The team playing at home has an "home advantage".

Then, all this is modeled for the entire season; each team gets an attack and a defense "strength".

The question I was asking myself was: can these values change during the season? I followed the Montreal Canadians this year. They were average during the season, but then had a great playoff run. Is that something you can measure? We're they significantly better during their playoff run? This is what I try to look into here.

Scores for both teams are modeled as follow: 

$$score1 \sim poisson(theta1)$$

$$score2 \sim poisson(theta2)$$

$$theta1 \sim exp(home+att[home]-def[away])$$

$$theta2 \sim exp(att[away]-def[home])$$

And the Stan implementation (also linked to on github above):

```
data {
  int<lower=0> nt; //number of teams
  int<lower=0> ng; //number of games
  int<lower=0> ht[ng]; //home team index
  int<lower=0> at[ng]; //away team index
  int<lower=0> s1[ng]; //score home team
  int<lower=0> s2[ng]; //score away team
  int<lower=0> np; //number of predicted games
  int<lower=0> htnew[np]; //home team index for prediction
  int<lower=0> atnew[np]; //away team index for prediction
}

parameters {
  real home; //home advantage
  vector[nt] att; //attack ability of each team
  vector[nt] def; //defence ability of each team
  //hyper parameters
  real mu_att;
  real<lower=0> tau_att;
  real mu_def;
  real<lower=0> tau_def;
}

transformed parameters {
  vector[ng] theta1; //score probability of home team
  vector[ng] theta2; //score probability of away team

  theta1 = exp(home + att[ht] - def[at]);
  theta2 = exp(att[at] - def[ht]);

}

model {
//hyper priors
mu_att ~ normal(0,0.1);
tau_att ~ normal(0,1);
mu_def ~ normal(0,0.1);
tau_def ~ normal(0,1);

//priors
att ~ normal(mu_att, tau_att);
def ~ normal(mu_def, tau_def);
home ~ normal(0,0.0001);


//likelihood
    s1 ~ poisson(theta1);
    s2 ~ poisson(theta2);
}

generated quantities {
//generate predictions
  vector[np] theta1new; //score probability of home team
  vector[np] theta2new; //score probability of away team
  real s1new[np]; //predicted score
  real s2new[np]; //predicted score


  theta1new = exp(home + att[htnew] - def[atnew]);
  theta2new = exp(att[atnew] - def[htnew]);

  s1new = poisson_rng(theta1new);
  s2new = poisson_rng(theta2new);
}
```

